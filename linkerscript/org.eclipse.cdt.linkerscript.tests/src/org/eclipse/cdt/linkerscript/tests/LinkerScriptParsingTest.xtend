/*
 * generated by Xtext 2.10.0
 */
package org.eclipse.cdt.linkerscript.tests

import com.google.inject.Inject
import java.util.HashMap
import java.util.Map
import org.eclipse.cdt.linkerscript.linkerScript.AssignmentHidden
import org.eclipse.cdt.linkerscript.linkerScript.AssignmentProvide
import org.eclipse.cdt.linkerscript.linkerScript.AssignmentProvideHidden
import org.eclipse.cdt.linkerscript.linkerScript.InputSection
import org.eclipse.cdt.linkerscript.linkerScript.InputSectionFile
import org.eclipse.cdt.linkerscript.linkerScript.InputSectionWild
import org.eclipse.cdt.linkerscript.linkerScript.LExpression
import org.eclipse.cdt.linkerscript.linkerScript.LinkerScript
import org.eclipse.cdt.linkerscript.linkerScript.OutputSection
import org.eclipse.cdt.linkerscript.linkerScript.OutputSectionAlignExpression
import org.eclipse.cdt.linkerscript.linkerScript.OutputSectionAlignWithInput
import org.eclipse.cdt.linkerscript.linkerScript.OutputSectionConstraintOnlyIfRO
import org.eclipse.cdt.linkerscript.linkerScript.OutputSectionTypeNoLoad
import org.eclipse.cdt.linkerscript.linkerScript.Statement
import org.eclipse.cdt.linkerscript.linkerScript.StatementAssert
import org.eclipse.cdt.linkerscript.linkerScript.StatementAssignment
import org.eclipse.cdt.linkerscript.linkerScript.StatementConstructors
import org.eclipse.cdt.linkerscript.linkerScript.StatementConstructorsSorted
import org.eclipse.cdt.linkerscript.linkerScript.StatementCreateObjectSymbols
import org.eclipse.cdt.linkerscript.linkerScript.StatementData
import org.eclipse.cdt.linkerscript.linkerScript.StatementFill
import org.eclipse.cdt.linkerscript.linkerScript.StatementInclude
import org.eclipse.cdt.linkerscript.linkerScript.StatementInputSection
import org.eclipse.cdt.linkerscript.linkerScript.StatementNop
import org.eclipse.cdt.linkerscript.linkerScript.Wildcard
import org.eclipse.cdt.linkerscript.linkerScript.WildcardSortAlign
import org.eclipse.cdt.linkerscript.linkerScript.WildcardSortAlignName
import org.eclipse.cdt.linkerscript.linkerScript.WildcardSortInitPriority
import org.eclipse.cdt.linkerscript.linkerScript.WildcardSortName
import org.eclipse.cdt.linkerscript.linkerScript.WildcardSortNameAlign
import org.eclipse.cdt.linkerscript.linkerScript.WildcardSortNone
import org.eclipse.xtext.junit4.InjectWith
import org.eclipse.xtext.junit4.XtextRunner
import org.eclipse.xtext.junit4.util.ParseHelper
import org.eclipse.xtext.junit4.validation.ValidationTestHelper
import org.hamcrest.FeatureMatcher
import org.hamcrest.Matcher
import org.junit.Test
import org.junit.runner.RunWith

import static org.hamcrest.Matchers.*
import static org.junit.Assert.*

@RunWith(XtextRunner)
@InjectWith(LinkerScriptInjectorProvider)
class LinkerScriptParsingTest {

	@Inject extension ParseHelper<LinkerScript> parseHelper
	@Inject extension ValidationTestHelper

//	def Matcher<InputSectionSpec> nameIs(String s) {
//		return new FeatureMatcher<InputSectionSpec, String>(equalTo(s), "string", "string") {
//			override protected String featureValueOf(InputSectionSpec actual) {
//				return actual.name
//			}
//		};
//	}
//
//	def Matcher<InputSectionSpec> excludesAre(Collection<String> excludes) {
//		return new FeatureMatcher<InputSectionSpec, Collection<String>>(equalTo(excludes), "strings", "strings") {
//			override protected Collection<String> featureValueOf(InputSectionSpec actual) {
//				return actual.excludes
//			}
//		};
//	}
	def Long reduce(LExpression exp) {
		return reduce(exp, emptyMap)
	}

	def Long reduce(LExpression exp, Map<String, Long> memorySize) {
		return reduce(exp, memorySize, emptyMap)
	}

	def Long reduce(LExpression exp, Map<String, Long> memorySize, Map<String, Long> variableValues) {
		val reducer = new LExpressionReducer(memorySize, variableValues)
		return reducer.reduceToLong(exp)
	}

//
//	@Test
//	def void inputSectionParsing() {
//		val model = '''
//			SECTIONS {
//				.output+name : {
//					*hello?assdf(SORT_BY_NAME(*), SORT_BY_ALIGNMENT(*), .text.* /*comment*/ 
//						a*b EXCLUDE_FILE(excluded_file1 excluded_file2) *)
//				}
//			}
//			
//		'''.parse
//		assertNotNull(model)
//		model.assertNoErrors
//		val sections = model.sections.sections
//		assertThat(sections.size, is(1))
//		val outputSection = sections.get(0)
//		assertThat(outputSection.name, is(".output+name"))
//// TODO: this is not right model yet
////		val inputNames = outputSection.inputNames
////		assertThat(inputNames.size, is(1))
////		val inputName = inputNames.get(0)
//		val inputFileName = outputSection.fileName
//		assertThat(inputFileName, is("*hello?assdf"))
//		val inputSections = outputSection.sections
//		val inputSectionSpecs = inputSections.sectionSpecs
//		assertThat(inputSectionSpecs, contains(nameIs("*"), nameIs("*"), nameIs(".text.*"), nameIs("a*b"), nameIs("*")))
//		assertThat(inputSectionSpecs,
//			contains(instanceOf(InputSectionSpecSortName), instanceOf(InputSectionSpecSortAlign),
//				instanceOf(InputSectionSpecSortNone), instanceOf(InputSectionSpecSortNone),
//				instanceOf(InputSectionSpecSortNone)))
//		assertThat(inputSectionSpecs,
//			contains(instanceOf(InputSectionSpecSortName), instanceOf(InputSectionSpecSortAlign),
//				instanceOf(InputSectionSpecSortNone), instanceOf(InputSectionSpecSortNone),
//				instanceOf(InputSectionSpecSortNone)))
//
//	}
	def OutputSection outputSection(CharSequence script) {
		val model = script.parse
		assertNotNull(model)
		model.assertNoErrors
		val sections = model.sections.sections
		assertThat(sections.size, is(1))
		val outputSection = sections.get(0)
		assertNotNull(outputSection)
		return outputSection
	}

	@Test
	def void outputSectionAllFields() {
		val outputSection = '''
			SECTIONS {
				.output+name 2*(3+4) (NOLOAD) : AT(2*(3+4)) ALIGN(2*(3+4)) SUBALIGN(2*(3+4))  ONLY_IF_RO {
				}>.dots.everywhere. AT>.dots.everywhere2. :.dots.everywhere3. :.dots.everywhere4. =2*(3+4),
			}
			
		'''.outputSection
		assertThat(outputSection.name, is(".output+name"))
		assertThat(outputSection.address.reduce, is(14L))
		assertThat(outputSection.type, instanceOf(OutputSectionTypeNoLoad))
		assertThat(outputSection.at.reduce, is(14L))
		assertThat(outputSection.align, instanceOf(OutputSectionAlignExpression))
		assertThat((outputSection.align as OutputSectionAlignExpression).exp.reduce, is(14L))
		assertThat(outputSection.subAlign.reduce, is(14L))
		assertThat(outputSection.constraint, instanceOf(OutputSectionConstraintOnlyIfRO))
		assertThat(outputSection.memory, is(".dots.everywhere."))
		assertThat(outputSection.atMemory, is(".dots.everywhere2."))
		assertThat(outputSection.phdrs, contains(".dots.everywhere3.", ".dots.everywhere4."))
		assertThat(outputSection.fill.reduce, is(14L))
	}

	@Test
	def void outputSectionName() {
		assertThat('''
			SECTIONS {
				.output+name : {
				},
			}
			
		'''.outputSection.name, is(".output+name"))
	}

	@Test
	def void outputSectionAddress() {
		assertThat('''
			SECTIONS {
				.output+name 2*(3+4) : {
				},
			}
			
		'''.outputSection.address.reduce, is(14L))
	}

	@Test
	def void outputSectionType() {
		assertThat('''
			SECTIONS {
				.output+name (NOLOAD) : {
				},
			}
			
		'''.outputSection.type, instanceOf(OutputSectionTypeNoLoad))
	}

	@Test
	def void outputSectionAtAddress() {
		assertThat('''
			SECTIONS {
				.output+name : AT(2*(3+4)) {
				},
			}
			
		'''.outputSection.at.reduce, is(14L))
	}

	@Test
	def void outputSectionAlign() {
		val sectionAlign = '''
			SECTIONS {
				.output+name : ALIGN(2*(3+4)) {
				},
			}
			
		'''.outputSection.align
		assertThat(sectionAlign, instanceOf(OutputSectionAlignExpression))
		assertThat((sectionAlign as OutputSectionAlignExpression).exp.reduce, is(14L))
	}

	@Test
	def void outputSectionAlignWithInput() {
		assertThat('''
			SECTIONS {
				.output+name : ALIGN_WITH_INPUT {
				},
			}
			
		'''.outputSection.align, instanceOf(OutputSectionAlignWithInput))
	}

	@Test
	def void outputSectionSubAlign() {
		assertThat('''
			SECTIONS {
				.output+name : SUBALIGN(2*(3+4)) {
				},
			}
			
		'''.outputSection.subAlign.reduce, is(14L))
	}

	@Test
	def void outputSectionConstraint() {
		assertThat('''
			SECTIONS {
				.output+name : ONLY_IF_RO {
				},
			}
			
		'''.outputSection.constraint, instanceOf(OutputSectionConstraintOnlyIfRO))
	}

	@Test
	def void outputSectionMemory() {
		assertThat('''
			SECTIONS {
				.output+name : {
				} >.dots.everywhere. ,
			}
			
		'''.outputSection.memory, is(".dots.everywhere."))
	}

	@Test
	def void outputSectionAtMemory() {
		assertThat('''
			SECTIONS {
				.output+name : {
				} AT>.dots.everywhere. ,
			}
			
		'''.outputSection.atMemory, is(".dots.everywhere."))
	}

	@Test
	def void outputSectionPHdrs() {
		assertThat('''
			SECTIONS {
				.output+name : {
				} :.dots.everywhere. :.dots.everywhere2.,
			}
			
		'''.outputSection.phdrs, contains(".dots.everywhere.", ".dots.everywhere2."))
	}

	@Test
	def void outputSectionFill() {
		assertThat('''
			SECTIONS {
				.output+name : {
				} =2*(3+4) ,
			}
			
		'''.outputSection.fill.reduce, is(14L))
	}

	def Statement statement(CharSequence statement) {
		val statements = ("SECTIONS { .name : { " + statement + " }}").outputSection.statements
		assertNotNull(statements)
		assertThat(statements.size, is(1))
		assertNotNull(statements.get(0))
		return statements.get(0)
	}

	@Test
	def void statementAssignmentAssign() {
		val assignment = ('''value = 2*(3+4);'''.statement as StatementAssignment).assignment
		assertThat(assignment.name, is('value'))
		assertThat(assignment.feature, is('='))
		assertThat(assignment.exp.reduce, is(14L))
	}

	@Test
	def void statementAssignmentMultiAssign() {
		for (assign : #['+=', '-=', '*=', '/=', '<<=', '>>=', '&=', '|=']) {
			val assignment = (('''value ''' + assign + ''' 2*(3+4);''').statement as StatementAssignment).assignment
			assertThat(assignment.name, is('value'))
			assertThat(assignment.feature, is(assign))
			assertThat(assignment.exp.reduce, is(14L))
		}
	}

	@Test
	def void statementAssignmentToDot() {
		val assignment = ('''. = 2*(3+4);'''.statement as StatementAssignment).assignment
		assertThat(assignment.name, is('.'))
		assertThat(assignment.feature, is('='))
		assertThat(assignment.exp.reduce, is(14L))
	}

	@Test
	def void statementAssignmentFromDot() {
		val assignment = ('''etext = .;'''.statement as StatementAssignment).assignment
		assertThat(assignment.name, is('etext'))
		assertThat(assignment.feature, is('='))
		assertThat(assignment.exp.reduce(emptyMap, #{'.' -> 14L}), is(14L))
	}

	@Test
	def void statementAssignmentFromSymbol() {
		val assignment = ('''etext = othertext;'''.statement as StatementAssignment).assignment
		assertThat(assignment.name, is('etext'))
		assertThat(assignment.feature, is('='))
		assertThat(assignment.exp.reduce(emptyMap, #{'othertext' -> 14L}), is(14L))
	}

	@Test
	def void statementAssignmentHidden() {
		val assignment = ('''HIDDEN(value = 2*(3+4));'''.statement as StatementAssignment).assignment
		assertThat(assignment, instanceOf(AssignmentHidden))
		assertThat(assignment.name, is('value'))
		assertThat(assignment.feature, is('='))
		assertThat(assignment.exp.reduce, is(14L))
	}

	@Test
	def void statementAssignmentProvide() {
		val assignment = ('''PROVIDE(value = 2*(3+4));'''.statement as StatementAssignment).assignment
		assertThat(assignment, instanceOf(AssignmentProvide))
		assertThat(assignment.name, is('value'))
		assertThat(assignment.feature, is('='))
		assertThat(assignment.exp.reduce, is(14L))
	}

	@Test
	def void statementAssignmentProvideHidden() {
		val assignment = ('''PROVIDE_HIDDEN(value = 2*(3+4));'''.statement as StatementAssignment).assignment
		assertThat(assignment, instanceOf(AssignmentProvideHidden))
		assertThat(assignment.name, is('value'))
		assertThat(assignment.feature, is('='))
		assertThat(assignment.exp.reduce, is(14L))
	}

	@Test
	def void statementAssignmentNop() {
		// TODO: We are creating objects for just ; how to get around that?
		assertThat(";".statement, instanceOf(StatementNop))
	}

	@Test
	def void statementCreateObjectSymbols() {
		assertThat("CREATE_OBJECT_SYMBOLS".statement, instanceOf(StatementCreateObjectSymbols))
	}

	@Test
	def void statementConstructors() {
		assertThat("CONSTRUCTORS".statement, instanceOf(StatementConstructors))
	}

	@Test
	def void statementConstructorsSorted() {
		assertThat("SORT_BY_NAME(CONSTRUCTORS)".statement, instanceOf(StatementConstructorsSorted))
	}

	@Test
	def void statementData() {
		for (size : #['BYTE', 'SHORT', 'LONG', 'QUAD', 'SQUAD']) {
			val data = ((size + "(2*(3+4))").statement as StatementData)
			assertThat(data.size, is(size))
			assertThat(data.data.reduce, is(14L))
		}
	}

	@Test
	def void statementFill() {
		val data = ("FILL(2*(3+4))".statement as StatementFill)
		assertThat(data.fill.reduce, is(14L))
	}

	@Test
	def void statementAssert() {
		val assertStatement = ('''ASSERT(2*(3+4), "Message")'''.statement as StatementAssert)
		assertThat(assertStatement.exp.reduce, is(14L))
		assertThat(assertStatement.message, is("Message"))
	}

	@Test
	def void statementAssertUnquoted() {
		val assertStatement = ('''ASSERT(2*(3+4), Message:Doesn_t-Have~To[Quoted])'''.statement as StatementAssert)
		assertThat(assertStatement.exp.reduce, is(14L))
		assertThat(assertStatement.message, is("Message:Doesn_t-Have~To[Quoted]"))
	}

	@Test
	def void statementInclude() {
		val assertStatement = ('''INCLUDE fileName!here'''.statement as StatementInclude)
		assertThat(assertStatement.filename, is("fileName!here"))
	}

	def InputSection inputSection(CharSequence inputSectionText) {
		val statement = inputSectionText.statement
		assertNotNull(statement)
		assertThat(statement, instanceOf(StatementInputSection))
		val inputSection = (statement as StatementInputSection).spec
		assertNotNull(inputSection)
		return inputSection
	}

	def InputSectionWild inputSectionWild(CharSequence inputSectionText) {
		val inputSectionWild = inputSectionText.inputSection
		assertThat(inputSectionWild, instanceOf(InputSectionWild))
		return inputSectionWild as InputSectionWild
	}

	def InputSectionFile inputSectionFile(CharSequence inputSectionText) {
		val inputSectionFile = inputSectionText.inputSection
		assertThat(inputSectionFile, instanceOf(InputSectionFile))
		return inputSectionFile as InputSectionFile
	}

	@Test
	def void inputSectionFile() {
		assertThat('filename'.inputSectionFile.file, is("filename"))
		assertThat('*'.inputSectionFile.file, is("*"))
		assertThat('"*.o"'.inputSectionFile.file, is("*.o"))
	// TODO assertThat('*.o'.inputSection.file, is("*.o"))
	}

	@Test
	def void inputSectionFileFlag() {
		val inputSection = 'INPUT_SECTION_FLAGS(FLAG1) filename'.inputSectionFile

		assertThat(inputSection.file, is("filename"))
		assertThat(inputSection.flags, contains("FLAG1"))
	}

	@Test
	def void inputSectionFileFlags() {
		val inputSection = 'INPUT_SECTION_FLAGS(FLAG1 & FLAG2) filename'.inputSectionFile

		assertThat(inputSection.file, is("filename"))
		assertThat(inputSection.flags, contains("FLAG1", "FLAG2"))
	}

	def Matcher<Wildcard> nameIs(String s) {
		return new FeatureMatcher<Wildcard, String>(equalTo(s), "string", "string") {
			override protected String featureValueOf(Wildcard actual) {
				return actual.name
			}
		};
	}

	@Test
	def void inputSectionWildFile() {
		assertThat('*(.text)'.inputSectionWild.wildFile, allOf(nameIs("*"), instanceOf(WildcardSortNone)))
		assertThat('filename(.text)'.inputSectionWild.wildFile, allOf(nameIs("filename"), instanceOf(WildcardSortNone)))
		assertThat('"*.o"(.text)'.inputSectionWild.wildFile, allOf(nameIs("*.o"), instanceOf(WildcardSortNone)))

		// single sort options
		assertThat('SORT(*)(.text)'.inputSectionWild.wildFile, allOf(nameIs("*"), instanceOf(WildcardSortName)))
		assertThat('SORT_BY_NAME(*)(.text)'.inputSectionWild.wildFile, allOf(nameIs("*"), instanceOf(WildcardSortName)))
		assertThat('SORT_BY_ALIGNMENT(*)(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortAlign)))
		assertThat('SORT_NONE(*)(.text)'.inputSectionWild.wildFile, allOf(nameIs("*"), instanceOf(WildcardSortNone)))
		assertThat('SORT_BY_INIT_PRIORITY(*)(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortInitPriority)))

		// legal combinations of sort options
		assertThat('SORT_BY_NAME(SORT_BY_NAME(*))(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortName)))
		assertThat('SORT_BY_ALIGNMENT(SORT_BY_ALIGNMENT(*))(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortAlign)))
		assertThat('SORT_BY_ALIGNMENT(SORT_BY_NAME(*))(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortAlignName)))
		assertThat('SORT_BY_NAME(SORT_BY_ALIGNMENT(*))(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortNameAlign)))

		// excludes
		assertThat('EXCLUDE_FILE(excluded)*(.text)'.inputSectionWild.wildFile.excludes, contains("excluded"))
		assertThat('EXCLUDE_FILE(excluded)*(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortNone)))
		assertThat('EXCLUDE_FILE(excluded1 excluded2)*(.text)'.inputSectionWild.wildFile.excludes,
			contains("excluded1", "excluded2"))
		assertThat('EXCLUDE_FILE(excluded1 excluded2)*(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortNone)))

		// sorted excludes
		assertThat('SORT(EXCLUDE_FILE(excluded)*)(.text)'.inputSectionWild.wildFile.excludes, contains("excluded"))
		assertThat('SORT(EXCLUDE_FILE(excluded)*)(.text)'.inputSectionWild.wildFile,
			allOf(nameIs("*"), instanceOf(WildcardSortName)))
	}

	@Test
	def void inputSectionWildSections() {
		assertThat('*(.text)'.inputSectionWild.sections, contains(nameIs(".text")))
		assertThat('*(.text .text.*)'.inputSectionWild.sections, contains(nameIs(".text"), nameIs(".text.*")))
		assertThat('*(.text, .text.*)'.inputSectionWild.sections, contains(nameIs(".text"), nameIs(".text.*")))
	}

	@Test
	def void inputSectionWildFlag() {
		val inputSection = 'INPUT_SECTION_FLAGS(FLAG1) *(.text)'.inputSectionWild

		assertThat(inputSection.wildFile, nameIs("*"))
		assertThat(inputSection.flags, contains("FLAG1"))
	}

	@Test
	def void inputSectionWildFlags() {
		val inputSection = 'INPUT_SECTION_FLAGS(FLAG1 & FLAG2) *(.text)'.inputSectionWild

		assertThat(inputSection.wildFile, nameIs("*"))
		assertThat(inputSection.flags, contains("FLAG1", "FLAG2"))
	}

	// TODO add tests for KEEP

	@Test
	def void starAndExclamationConflicts() {
		val model = '''
			MEMORY {
				FIRST (R ! W ! I) : ORIGIN = 0, LENGTH = 1
				SECOND (R ! W ! I) : ORIGIN = 0, LENGTH = 1*1
				THIRD (R ! W ! I) : ORIGIN = 0, LENGTH = 1*1
			}
		'''.parse
		assertNotNull(model)
		model.assertNoErrors
	}

	@Test
	def void memoryParsing() {
		val model = '''
			MEMORY {
				"FIRST MEMORY" : ORIGIN = A1ABx, LENGTH = (1+2)*3 
				OTHER : ORIGIN = A1ABx, LENGTH = ALIGN(LENGTH("FIRST MEMORY"), 1000) 
				.dots.everywhere. : ORIGIN = A1ABx, LENGTH = LENGTH(OTHER) 
				TRICKY : ORIGIN = A1ABx, LENGTH = A1ABx +3 /* space required before + */
				MIXED_EXPRESSION_AND_NAME : ORIGIN = 0x0, LENGTH = ALIGN((5000+2)*3, LENGTH(RAM3)) 
				SAMELINE1 : ORIGIN = 0x0, LENGTH = 2 + 3 SAMELINE2 : ORIGIN = 0x0, LENGTH = 3 + 4
				MEMATTRIBS (RW!I) : ORIGIN = 5, LENGTH = 6
			}
		'''.parse
		assertNotNull(model)
		model.assertNoErrors
		val memories = model.memories.memories
		assertThat(memories.size, is(8))

		// We are not setting up real memory sizes, so 
		// we preload with known values here
		val Map<String, Long> memorySizes = new HashMap();
		memorySizes.put("FIRST MEMORY", 1234L)
		memorySizes.put("OTHER", 5678L)
		memorySizes.put("RAM3", 5000L)
		val reducer = new LExpressionReducer(memorySizes)

		val memory0 = memories.get(0)
		assertThat(memory0.name, is("FIRST MEMORY"))
		assertThat(reducer.reduceToLong(memory0.origin), is(0xA1AB#L))
		assertThat(reducer.reduceToLong(memory0.length), is(9L))

		val memory1 = memories.get(1)
		assertThat(memory1.name, is("OTHER"))
		assertThat(reducer.reduceToLong(memory1.origin), is(0xA1AB#L))
		assertThat(reducer.reduceToLong(memory1.length), is(2000L))

		val memory2 = memories.get(2)
		assertThat(memory2.name, is(".dots.everywhere."))
		assertThat(reducer.reduceToLong(memory2.origin), is(0xA1AB#L))
		assertThat(reducer.reduceToLong(memory2.length), is(5678L))

		val memory3 = memories.get(3)
		assertThat(memory3.name, is("TRICKY"))
		assertThat(reducer.reduceToLong(memory3.origin), is(0xA1AB#L))
		assertThat(reducer.reduceToLong(memory3.length), is(0xA1AB#L + 3))

		val memory4 = memories.get(4)
		assertThat(memory4.name, is("MIXED_EXPRESSION_AND_NAME"))
		assertThat(reducer.reduceToLong(memory4.origin), is(0L))
		assertThat(reducer.reduceToLong(memory4.length), is(20000L))

		val memory5 = memories.get(5)
		assertThat(memory5.name, is("SAMELINE1"))
		assertThat(reducer.reduceToLong(memory5.origin), is(0L))
		assertThat(reducer.reduceToLong(memory5.length), is(5L))

		val memory6 = memories.get(6)
		assertThat(memory6.name, is("SAMELINE2"))
		assertThat(reducer.reduceToLong(memory6.origin), is(0L))
		assertThat(reducer.reduceToLong(memory6.length), is(7L))

		val memory7 = memories.get(7)
		assertThat(memory7.name, is("MEMATTRIBS"))
		assertThat(reducer.reduceToLong(memory7.origin), is(5L))
		assertThat(reducer.reduceToLong(memory7.length), is(6L))
		// TODO this tests current lack of simplifier on mem atttributes
		assertThat(memory7.attr, is("(RW!I)"))
	}

}
